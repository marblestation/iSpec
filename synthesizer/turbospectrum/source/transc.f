C
      SUBROUTINE TRANSC
C
C SCATTR SOLVES THE TRANSFER EQUATION INCLUDING CONTINUUM SCATTERING
C IN THE EDDINGTON APPROXIMATION, I.E., USING ONLY ONE RAY.
C 'ERROR' IS THE INHOMOGENEOUS TERM OF THE EQUATION, AND 'P' GIVES THE
C ESTIMATED MEAN INTENSITY CORRECTION (FJ*P). TRANSC CALCULATES THE MATRIX
C ELEMENTS FOR SCATTR.
C 79.06.21 *NORD*
C
C
      INCLUDE 'spectrum.inc'
C
      COMMON /CTRAN/X(NDP),S(NDP),BPLAN(NDP),XJ(NDP),XH(NDP),XK(NDP)
     & ,FJ(NDP),SOURCE(NDP),TAUS(NDP),DTAUS(NDP),JTAU0,JTAU1,ISCAT
      COMMON /TAUC/TAU(NDP),DTAULN(NDP),JTAU
      COMMON /SPACE2/ERROR(NDP),FACT(NDP),DSO(NDP),
     &  P(NDP),SP1(NDP),SP2(NDP),SP3(NDP),
     &  DUM(NDP,NRAYS,3),AD(NDP,NRAYS),BD(NDP,NRAYS),EX(NRAYS),
     &  NIMPAC,KIMPAC(NRAYS),PIMPAC(NRAYS),MMU(NDP),
     &  TAUT(NDP),DTAUT(NDP),
     &  PFEAU(NRAYS,NDP),XMU(NRAYS,NDP)
      COMMON /CSPHER/NCORE,DIFLOG,RADIUS,RR(NDP)
C
C MU LOOP
      NTAU=KIMPAC(ISCAT)
      NTAU1=NTAU-1
C
C CALCULATE TAUS ALONG THE RAY, SPIRAL AT EDDINGTON ANGLE AT DEPTH.
      Z=SQRT(RR(JTAU0)**2-PIMPAC(ISCAT)**2)
      ZOLD=Z
      DO 101 K=2,JTAU
      IF (JTAU-K+2.GT.JTAU0) GO TO 103
      Z=SQRT(RR(JTAU-K+1)**2-PIMPAC(ISCAT)**2)
      DZ=Z-ZOLD
      DZDR=DZ/(RR(JTAU-K+1)-RR(JTAU-K+2))
      GO TO 104
103   DZDR=1.732
104   DTAUS(JTAU-K+2)=DZDR*0.5*(X(JTAU-K+1)+S(JTAU-K+1)
     & +X(JTAU-K+2)+S(JTAU-K+2))*(TAU(JTAU-K+2)-TAU(JTAU-K+1))
101   ZOLD=Z
C
      TAUS(1)=DZDR*(X(1)+S(1))*TAU(1)
C
C K=1
      A=1./DTAUS(2)
      B=A**2
      SP2(1)=1.-FJ(1)*S(1)/(X(1)+S(1))+2.*A
      SP3(1)=-2.*B
      T=TAUS(1)
      EX(ISCAT)=TAUS(1)*(1.-0.5*TAUS(1)*(1.-0.333333*TAUS(1)))
      IF (TAUS(1).GT.0.1) EX(ISCAT)=1.-EXP(-TAUS(1))
      SP2(1)=SP2(1)-2.*A*EX(ISCAT)*FJ(1)*S(1)/(X(1)+S(1))
      SP2(1)=SP2(1)/(1.+2.*A*EX(ISCAT))
      SP3(1)=SP3(1)/(1.+2.*A*EX(ISCAT))
C
C K=2,NTAU-1
      DO 100 K=2,NTAU1
      DTAUC=0.5*(DTAUS(K)+DTAUS(K+1))
      SP1(K)=-1./(DTAUS(K)*DTAUC)
      SP2(K)=1.-FJ(K)*S(K)/(X(K)+S(K))
100   SP3(K)=-1./(DTAUS(K+1)*DTAUC)
C
C K=NTAU
      SP1(NTAU)=0.0
      SP2(NTAU)=X(NTAU)/(X(NTAU)+S(NTAU))
      SP3(NTAU)=0.0
C
C ELIMINATE SUBDIAGONAL
      DO 120 K=1,NTAU1
      SP1(K)=-SP1(K+1)/(SP2(K)-SP3(K))
      SP2(K+1)=SP2(K+1)+SP1(K)*SP2(K)
120   SP2(K)=SP2(K)-SP3(K)
121   CONTINUE
      RETURN
C--------------------------------------------------------------------
C
      ENTRY SCATTR
C
C MU LOOP
      NTAU=KIMPAC(ISCAT)
      NTAU1=NTAU-1
C
C ACCUMULATE RIGHT HAND SIDE
      P(1)=ERROR(1)
      DO 150 K=1,NTAU1
150   P(K+1)=ERROR(K+1)+SP1(K)*P(K)
C
C BACKSUBSTITUTE
      P(NTAU)=P(NTAU)/SP2(NTAU)
      DO 160 K=1,NTAU1
160   P(NTAU-K)=(P(NTAU-K)-SP3(NTAU-K)*P(NTAU-K+1))/SP2(NTAU-K)
C
      RETURN
      END
